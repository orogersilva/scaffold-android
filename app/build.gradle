plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
}

android {

    namespace 'com.orogersilva.scaffoldandroid'

    compileSdk buildConfig.compileSdk

    defaultConfig {

        applicationId "com.orogersilva.scaffoldandroid"

        minSdk buildConfig.minSdk
        targetSdk buildConfig.targetSdk

        versionCode appVersion.versionCode
        versionName appVersion.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    lintOptions {

        abortOnError true
        checkDependencies true
        checkReleaseBuilds false
        htmlOutput file("lint-report.html")
        htmlReport false
        lintConfig file("${project.rootDir}/quality/lint.xml")
        warningsAsErrors true
        xmlReport true
    }

    signingConfigs {

        release {

            storeFile rootProject.file('keystore/release.jks')
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {

        debug {

            testCoverageEnabled false

            debuggable true

            buildConfigField "Boolean", "IS_DEBUG_BUILD", "true"
            buildConfigField "Boolean", "IS_RELEASE_BUILD", "false"

            applicationIdSuffix ".dev"
            versionNameSuffix '-DEV'
        }

        release {

            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "Boolean", "IS_DEBUG_BUILD", "false"
            buildConfigField "Boolean", "IS_RELEASE_BUILD", "true"

            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidComposeCompilerVersion.get()
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation project(':feature:new-feature')

    api platform(libs.android.compose.billOfMaterials)

    implementation libs.android.hilt.navigationCompose

    implementation libs.dagger.hilt.android
    kapt libs.dagger.hilt.compiler
    debugImplementation libs.leakCanary.android
    implementation libs.timber.core
}